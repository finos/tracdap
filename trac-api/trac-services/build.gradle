/*
 * Copyright 2020 Accenture Global Solutions Limited
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "com.google.protobuf"
    id "java-library"
    id "idea"
}

apply from: "${rootProject.projectDir.path}/gradle/base-java.gradle"


sourceSets {
    main {
        java {
            // Include generated code from protoc / gRPC
            srcDirs "${project.buildDir}/generated-sources/main/java"
            srcDirs "${project.buildDir}/generated-sources/main/grpc"
        }
    }
}


dependencies {

    api project(':trac-api-metadata')

    // Protoc generates code using the 'Generated' annotation from this package
    api group: 'javax.annotation', name: 'javax.annotation-api', version: "$javax_annotation_version"

    // Core services API uses gRPC / Protobuf stubs
    api group: 'io.grpc', name: 'grpc-stub', version: "$grpc_version"
    api group: 'io.grpc', name: 'grpc-protobuf', version: "$grpc_version"

    // Google APIs framework for REST APIs
    implementation group: 'com.google.api.grpc', name: 'proto-google-common-protos', version: "$gapi_version"
}


protobuf {

    protoc {
        artifact = "com.google.protobuf:protoc:$proto_version"
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpc_version}"
        }
    }

    generateProtoTasks.generatedFilesBaseDir = "${project.buildDir}/generated-sources"

    generateProtoTasks {
        all().each { task ->

            task.builtins {
                java { }
            }

            task.plugins {
                grpc { }
            }
        }}
}
