/*
 * Copyright 2020 Accenture Global Solutions Limited
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "com.google.protobuf"
    id "java-library"
    id "idea"  // IDEA plugin is required to make IDEA work properly with protobuf
}

apply from: "${rootProject.projectDir.path}/gradle/base-java.gradle"


description = "TRAC D.A.P. platform API, contains service API definitions for the TRAC platform services"

protobuf {

    protoc {
        artifact = "com.google.protobuf:protoc:$proto_version"
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpc_version}"
        }
    }

    generateProtoTasks.generatedFilesBaseDir = "${project.buildDir}/generated-sources"

    generateProtoTasks.all().configureEach {

        builtins {
            java { }
        }

        plugins {
            grpc { }
        }
    }
}

sourceSets {
    main {
        java {
            // Include generated code from protoc / gRPC
            srcDirs "${project.buildDir}/generated-sources/main/java"
            srcDirs "${project.buildDir}/generated-sources/main/grpc"
        }
    }
}

dependencies {

    // Core services API uses gRPC / Protobuf stubs
    api "io.grpc:grpc-stub"
    api "io.grpc:grpc-protobuf"
    api "com.google.protobuf:protobuf-java"

    api project(":tracdap-api-metadata")

    // Google APIs framework for REST APIs
    // Do not pull in the JavaX annotation API (it has an incompatible license)
    api (group: 'com.google.api.grpc', name: 'grpc-google-common-protos', version: "$gapi_version") {
        exclude group: "javax.annotation", module: "javax.annotation-api"
    }

    // JavaX "Generated" annotation is still required by gRPC generated code
    implementation project(':tracdap-lib-annotations')
}

// Javadoc gives a lot of warnings on the protobuf generated code
// Turn these warnings off, they're noisy and obscure real Javadoc warnings from TRAC's own code
tasks.withType(Javadoc).configureEach {
    options.addStringOption('Xdoclint:none', '-quiet')
}
