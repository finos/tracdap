
def publishModule(Project project) {

    publishing {
        publications {

            "$project.name" (MavenPublication) {

                from project.components.java

                groupId = project.group
                artifactId = project.name
                version = project.version

                pom {

                    name = project.name
                    description = project.description
                    url = 'https://tracdap.finos.org'

                    licenses {
                        license {
                            name = 'Apache License, Version 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    scm {
                        connection = 'scm:git:git://github.com/finos/tracdap.git'
                        developerConnection = 'scm:git:ssh://github.com/finos/tracdap.git'
                        url = 'https://github.com/finos/tracdap'
                    }

                    issueManagement {
                        system = "GitHub Issues"
                        url = "https://github.com/finos/tracdap/issues"
                    }

                    developers {
                        developer {
                            name = 'Martin Traverse'
                            email = 'martin@fintrac.co.uk'
                            organization = 'finTRAC Limited'
                            organizationUrl = 'https://fintrac.co.uk/'
                        }
                    }
                }
            }

        }
    }

    var disableSigning = project.findProperty("disableSigning")

    if (disableSigning != "true") {
        signing {
            sign publishing.publications[project.name]
        }
    }
}

ext {
    publishModule = { Project project -> this.publishModule(project) }
}
