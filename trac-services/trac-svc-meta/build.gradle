/*
 * Copyright 2020 Accenture Global Solutions Limited
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "application"
    id "idea"
}

apply from: "${rootProject.projectDir.path}/gradle/base-java.gradle"
apply from: "${rootProject.projectDir.path}/gradle/functions.gradle"

ext.addVersionInfo(sourceSets.getByName("main"), "TRAC Metadata Service")


application {
    mainClassName = 'com.accenture.trac.svc.meta.TracMetadataService'
    tasks.run.workingDir = rootProject.projectDir
}

sourceSets {
    aws {}
    gcp {}
}

dependencies {

    implementation project(':trac-api-metadata')
    implementation project(':trac-api-services')
    implementation project(':trac-lib-common')
    implementation project(':trac-lib-db')

    // Add runtime dependencies for SQL drivers
    // Individual drivers can be enabled/disabled via gradle/plugins.gradle
    // There may be licensing implications to enabling certain JDBC drivers
    // See the NOTICE file for more information on licensing restrictions
    runtimeOnly project(':sql-drivers')

    awsRuntimeOnly project(':aws-config')
    gcpRuntimeOnly project(':gcp-config')

    // Core framework - gRPC on Netty
    implementation group: 'io.netty', name: 'netty-common', version: "$netty_version"
    implementation group: 'io.netty', name: 'netty-codec-http', version: "$netty_version"
    implementation group: 'io.netty', name: 'netty-codec-http2', version: "$netty_version"
    implementation group: 'io.netty', name: 'netty-handler-proxy', version: "$netty_version"
    implementation group: 'io.grpc', name: 'grpc-netty', version: "$grpc_version"

    // CLI library for processing startup args (part of the .config package)
    implementation group: 'commons-cli', name: 'commons-cli', version: "$commons_cli_version"

    // Logging
    implementation group: 'org.slf4j', name: 'slf4j-api', version: "$slf4j_version"
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: "$log4j_version"
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: "$log4j_version"
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: "$log4j_version"

    // Migration support for JUnit 4 Rules, provided by gRPC for managing test service instances
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-migrationsupport', version: "$junit_version"

    testImplementation (group: 'io.grpc', name: 'grpc-testing', version: "$grpc_version") {
        // Do not pull in JUnit 4, use migration support from JUnit 5 instead
        exclude group: 'junit', module: 'junit'
    }

    // Always pull in the H2 JDBC driver as a test dependency, since it used for unit tests
    testImplementation group: 'com.h2database', name: 'h2', version: "$h2_version"
}

// Bring DDL files into test JAR as resources
processTestResources {

    from "${project(':trac-svc-meta').projectDir.path}/src/schema"
}

sourceSets.each { cloudProvider ->

    if (cloudProvider.name != "main" && cloudProvider.name != "test") {

        // Only build drivers that are enabled in gradle/plugins.gradle or using the matching env variable

        def key = (String) "${cloudProvider.name}_config"
        def buildDriver = isFeatureEnabled(key)

        if (buildDriver) {

            println("Enabling cloud plugin: ${cloudProvider.name}")

            dependencies {
                implementation cloudProvider.runtimeClasspath
            }
        }
    }
}