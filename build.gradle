/*
 * Copyright 2020 Accenture Global Solutions Limited
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "org.owasp.dependencycheck" version "6.4.1.1"
    id 'com.github.jk1.dependency-license-report' version '2.0'
    id 'idea'
}

import com.github.jk1.license.filter.*

apply from: 'gradle/versions.gradle'
apply from: 'gradle/functions.gradle'

def versionNumber = ext.getVersionFromGit()

allprojects {

    group 'com.accenture.trac'
    version versionNumber
}

subprojects {

    buildDir = rootProject.buildDir.path + '/modules/' + project.name
}

dependencyCheck {

    data {
        directory = rootProject.buildDir.path + '/nvd/'
    }

    suppressionFiles = ["$projectDir/dev/vulnerabilities/false-positives.xml"]

    // CVSS score < 4 is "low severity" according to NIST
    // https://nvd.nist.gov/vuln-metrics/cvss

    failBuildOnCVSS = 4
}

licenseReport {

    allowedLicensesFile = new File("$projectDir/dev/licenses/permitted-licenses.json")

    filters = [
            new LicenseBundleNormalizer(createDefaultTransformationRules: true),
            new LicenseBundleNormalizer(bundlePath: "$projectDir/dev/licenses/license-normalization.json")]
}
