/*
 * Copyright 2020 Accenture Global Solutions Limited
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


/**
 * The sql-drivers project adds driver dependencies as configured in gradle/plugins.gradle
 *
 * Any enabled drivers may only be used in accordance with their respective licenses.
 * Please see the NOTICE file for legal information on the use of these drivers
 *
 * SQL drivers are not a compile-time dependency of the TRAC services
 * Instead, JDBC drivers can be added to the classpath at launch time
 * This allows TRAC to be distributed independently of any SQL driver
 *
 * For development and testing, SQL drivers can be used easily by linking
 * against the sql-drivers project as a runtime dependency. The build system will
 * try to turn this on automatically if it detects TRAC is running from an IDE.
 */

plugins {
    id "java-library"
}

apply from: "${rootProject.projectDir.path}/gradle/base-java.gradle"
apply from: "${rootProject.projectDir.path}/gradle/plugins.gradle"


// Set up source sets and dependencies for all supported SQL dialects

sourceSets {

    h2 {}
    mysql {}
    mariadb {}
    postgresql {}
    sqlserver {}
    oracle {}
}

dependencies {

    h2RuntimeOnly group: 'com.h2database', name: 'h2', version: "$h2_version"

    // Do not let MySQL pull in protobuf! We want to use the version from the core TRAC libs
    mysqlRuntimeOnly (group: 'mysql', name: 'mysql-connector-java', version: "$mysql_version") {
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
    }

    mariadbRuntimeOnly group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: "$mariadb_version"
    postgresqlRuntimeOnly group: 'org.postgresql', name: 'postgresql', version: "$postgresql_version"
    sqlserverRuntimeOnly group: 'com.microsoft.sqlserver', name: 'mssql-jdbc', version: "$sqlserver_version"
    oracleRuntimeOnly group: 'com.oracle.database.jdbc', name: 'ojdbc10', version: "$oracle_version"
}


// Skeleton tasks for packing plugins

configurations {
    packPlugins
}

task packPlugins {

}

def packPlugin(SourceSet dialect) {

    def pluginDir = "${rootProject.buildDir.path}/plugins/sql.${dialect.name}"
    mkdir(pluginDir)

    copy {
        from dialect.runtimeClasspath
        into pluginDir
    }
}


// Lock for the enabled driver plugins and switch them on

sourceSets.each { dialect ->

    if (dialect.name != "main" && dialect.name != "test") {

        // Only build drivers that are enabled in gradle/plugins.gradle or using the matching env variable

        def env = System.getenv()
        def buildKey = (String) "BUILD_sql_${dialect.name}"
        def buildDriver = false

        if (project.hasProperty("BUILD_ALL_PLUGINS") && project.property("BUILD_ALL_PLUGINS"))
            buildDriver = true

        if (project.hasProperty(buildKey) && project.property(buildKey))
            buildDriver = true

        if (env.containsKey(buildKey) && Boolean.parseBoolean(env.get(buildKey)))
            buildDriver = true

        if (buildDriver) {

            println("Enabling SQL driver: ${dialect.name}")

            // API dependency allows drivers to be used by linking the sql-drivers project
            dependencies {
                api dialect.runtimeClasspath
            }

            // packPlugin task is used to build plugins for deployment or distribution
            tasks.create("packPlugin_sql.${dialect.name}", { -> packPlugin(dialect) })
            tasks.packPlugins.dependsOn("packPlugin_sql.${dialect.name}")
        }

    }
}
