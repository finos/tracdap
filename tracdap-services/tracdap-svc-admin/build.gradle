/*
 * Licensed to the Fintech Open Source Foundation (FINOS) under one or
 * more contributor license agreements. See the NOTICE file distributed
 * with this work for additional information regarding copyright ownership.
 * FINOS licenses this file to you under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with the
 * License. You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "application"
}

apply from: "${rootProject.projectDir.path}/gradle/base-java.gradle"
apply from: "${rootProject.projectDir.path}/gradle/functions.gradle"
apply from: "${rootProject.projectDir.path}/gradle/plugins.gradle"


description = "TRAC D.A.P. admin service, stateless service component to answer the TRAC admin API"

application {

    mainClass.set("org.finos.tracdap.svc.admin.TracAdminService")

    // JVM flags needed for Arrow / Netty / gRPC to access native memory
    applicationDefaultJvmArgs = [
        "--add-opens", "java.base/jdk.internal.misc=ALL-UNNAMED",
        "--add-opens", "java.base/java.nio=ALL-UNNAMED",
        "-Dio.netty.tryReflectionSetAccessible=true"
    ]

    // Working dir when running in the IDE
    tasks.run.workingDir = rootProject.projectDir
}

ext.addVersionInfo(sourceSets.named("main").get(), "TRAC Admin Service")

dependencies {

    // Service API to implement
    implementation project(':tracdap-api-services')

    // TRAC library dependencies
    implementation project(':tracdap-lib-meta')
    implementation project(':tracdap-lib-common')
    implementation project(':tracdap-lib-validation')

    // The DB library can pull in runtime dependencies for SQL drivers
    // Individual drivers can be enabled/disabled via gradle/plugins.gradle
    // JARs published to Maven do not have any SQL dependencies
    // There may be licensing implications to enabling certain JDBC drivers
    // See the NOTICE file for more information on licensing restrictions

    implementation project(':tracdap-lib-db')

    // Core framework - gRPC on Netty
    implementation group: 'io.grpc', name: 'grpc-netty', version: "$grpc_version"

    testImplementation project(':tracdap-lib-test')
    testImplementation project(':tracdap-svc-meta')
}

// Add any plugin dependencies enabled at build time
// Note SQL driver plugins are enabled in tracdap-lib-db
ext.enableBuildTimePlugins(["config"])

// Compiling dependent targets does not require processResources to be executed by default
// E.g. running tests from the IDE will not trigger processResources - this can cause confusion!
// This dependency ensures resources are always processed, even for partial builds
compileJava.dependsOn(processResources)

distributions {
    main {
        contents {
            into("") { from("${rootDir}/dist/template") }
        }
    }
}

tasks.withType(CreateStartScripts).configureEach {

    unixStartScriptGenerator.template = resources.text.fromFile("${rootDir}/dist/scripts/service-ctrl.sh")
    windowsStartScriptGenerator.template = resources.text.fromFile("${rootDir}/dist/scripts/service-ctrl.bat")

    // Default config file location
    startScripts { doLast {
        unixScript.text = unixScript.text.replace("<DEFAULT_CONFIG_FILE>", "trac-platform.yaml")
        windowsScript.text = windowsScript.text.replace("<DEFAULT_CONFIG_FILE>", "trac-platform.yaml")
    }}
}
