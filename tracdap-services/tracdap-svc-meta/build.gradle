/*
 * Copyright 2020 Accenture Global Solutions Limited
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "application"
}

apply from: "${rootProject.projectDir.path}/gradle/base-java.gradle"
apply from: "${rootProject.projectDir.path}/gradle/functions.gradle"
apply from: "${rootProject.projectDir.path}/gradle/plugins.gradle"


description = "TRAC D.A.P. metadata service, stateless service component to answer the TRAC metadata API"

application {

    mainClass.set("org.finos.tracdap.svc.meta.TracMetadataService")

    // JVM flags needed for Arrow / Netty / gRPC to access native memory
    applicationDefaultJvmArgs = [
        "--add-opens", "java.base/jdk.internal.misc=ALL-UNNAMED",
        "--add-opens", "java.base/java.nio=ALL-UNNAMED",
        "-Dio.netty.tryReflectionSetAccessible=true"
    ]

    // Working dir when running in the IDE
    tasks.run.workingDir = rootProject.projectDir
}

ext.addVersionInfo(sourceSets.getByName("main"), "TRAC Metadata Service")

dependencies {

    implementation project(':tracdap-api-metadata')
    implementation project(':tracdap-api-config')
    implementation project(':tracdap-api-services')
    implementation project(':tracdap-lib-common')
    implementation project(':tracdap-lib-validation')

    // The DB library can pull in runtime dependencies for SQL drivers
    // Individual drivers can be enabled/disabled via gradle/plugins.gradle
    // JARs published to Maven do not have any SQL dependencies
    // There may be licensing implications to enabling certain JDBC drivers
    // See the NOTICE file for more information on licensing restrictions

    implementation project(':tracdap-lib-db')

    // Core framework - gRPC on Netty
    implementation group: 'io.netty', name: 'netty-common', version: "$netty_version"
    implementation group: 'io.netty', name: 'netty-codec-http', version: "$netty_version"
    implementation group: 'io.netty', name: 'netty-codec-http2', version: "$netty_version"
    implementation group: 'io.netty', name: 'netty-handler-proxy', version: "$netty_version"
    implementation group: 'io.grpc', name: 'grpc-netty', version: "$grpc_version"

    testImplementation project(':tracdap-lib-test')

    // Migration support for JUnit 4 Rules, provided by gRPC for managing test service instances
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-migrationsupport', version: "$junit_version"

    testImplementation (group: 'io.grpc', name: 'grpc-testing', version: "$grpc_version") {
        // Do not pull in JUnit 4, use migration support from JUnit 5 instead
        exclude group: 'junit', module: 'junit'
    }

    // Always pull in the H2 JDBC driver as a test dependency, since it used for unit tests
    testImplementation group: 'com.h2database', name: 'h2', version: "$h2_version"
}

// Add any plugin dependencies enabled at build time
// Note SQL driver plugins are enabled in tracdap-lib-db
ext.enableBuildTimePlugins(["config"])

// Compiling dependent targets does not require processResources to be executed by default
// E.g. running tests from the IDE will not trigger processResources - this can cause confusion!
// This dependency ensures resources are always processed, even for partial builds
compileJava.dependsOn(processResources)

// Bring DDL files into test JAR as resources
processTestResources {

    from "${project(':tracdap-svc-meta').projectDir.path}/src/schema"
}

distributions {
    main {
        contents {
            into("") { from("${rootDir}/dist/template") }
        }
    }
}

tasks.withType(CreateStartScripts) {

    unixStartScriptGenerator.template = resources.text.fromFile("${rootDir}/dist/scripts/service-ctrl.sh")
    windowsStartScriptGenerator.template = resources.text.fromFile("${rootDir}/dist/scripts/service-ctrl.bat")

    // Default config file location
    startScripts { doLast {
        unixScript.text = unixScript.text.replace("<DEFAULT_CONFIG_FILE>", "trac-platform.yaml")
        windowsScript.text = windowsScript.text.replace("<DEFAULT_CONFIG_FILE>", "trac-platform.yaml")
    }}
}
