/*
 * Copyright 2020 Accenture Global Solutions Limited
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "application"
}

apply from: "${rootProject.projectDir.path}/gradle/base-java.gradle"
apply from: "${rootProject.projectDir.path}/gradle/functions.gradle"


description = "TRAC D.A.P. metadata service, stateless service component to answer the TRAC metadata API"

ext.addVersionInfo(sourceSets.getByName("main"), "TRAC Metadata Service")

application {

    mainClass.set("org.finos.tracdap.svc.meta.TracMetadataService")
    tasks.run.workingDir = rootProject.projectDir

    // JVM flags needed for Arrow / Netty / gRPC to access native memory
    tasks.run.jvmArgs("--add-opens", "java.base/jdk.internal.misc=ALL-UNNAMED")
    tasks.run.jvmArgs("--add-opens", "java.base/java.nio=ALL-UNNAMED")
    tasks.run.systemProperty("io.netty.tryReflectionSetAccessible", true)
}

dependencies {

    implementation project(':tracdap-api-metadata')
    implementation project(':tracdap-api-config')
    implementation project(':tracdap-api-services')
    implementation project(':tracdap-lib-common')
    implementation project(':tracdap-lib-db')
    implementation project(':tracdap-lib-validation')

    // Add runtime dependencies for SQL drivers
    // Individual drivers can be enabled/disabled via gradle/plugins.gradle
    // There may be licensing implications to enabling certain JDBC drivers
    // See the NOTICE file for more information on licensing restrictions
    runtimeOnly project(':sql-drivers')

    // Core framework - gRPC on Netty
    implementation group: 'io.netty', name: 'netty-common', version: "$netty_version"
    implementation group: 'io.netty', name: 'netty-codec-http', version: "$netty_version"
    implementation group: 'io.netty', name: 'netty-codec-http2', version: "$netty_version"
    implementation group: 'io.netty', name: 'netty-handler-proxy', version: "$netty_version"
    implementation group: 'io.grpc', name: 'grpc-netty', version: "$grpc_version"

    testImplementation project(':tracdap-lib-test')

    // Migration support for JUnit 4 Rules, provided by gRPC for managing test service instances
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-migrationsupport', version: "$junit_version"

    testImplementation (group: 'io.grpc', name: 'grpc-testing', version: "$grpc_version") {
        // Do not pull in JUnit 4, use migration support from JUnit 5 instead
        exclude group: 'junit', module: 'junit'
    }

    // Always pull in the H2 JDBC driver as a test dependency, since it used for unit tests
    testImplementation group: 'com.h2database', name: 'h2', version: "$h2_version"
}

ext.pluginDependency("AWS_CONFIG", project(":aws-config"))
ext.pluginDependency("GCP_CONFIG", project(":gcp-config"))


// Bring DDL files into test JAR as resources
processTestResources {

    from "${project(':tracdap-svc-meta').projectDir.path}/src/schema"
}
