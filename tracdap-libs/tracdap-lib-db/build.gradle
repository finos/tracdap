/*
 * Copyright 2020 Accenture Global Solutions Limited
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "java-library"
}

apply from: "${rootProject.projectDir.path}/gradle/base-java.gradle"
apply from: "${rootProject.projectDir.path}/gradle/functions.gradle"
apply from: "${rootProject.projectDir.path}/gradle/plugins.gradle"


description = "TRAC D.A.P. library for working with SQL databases"

dependencies {

    implementation project(':tracdap-lib-common')

    // Hikari DBCP2 connection pool
    implementation (group: 'com.zaxxer', name: 'HikariCP', version: "$hikari_cp_version") {
        // Use the provided version of SLF4J, do not pull in the version from Hikari
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
}



// Set up source sets and dependencies for all supported SQL dialects

sourceSets {

    h2 {}
    mysql {}
    mariadb {}
    postgresql {}
    sqlserver {}
    oracle {}
}

dependencies {

    h2RuntimeOnly group: 'com.h2database', name: 'h2', version: "$h2_version"

    // Do not let MySQL pull in protobuf! We want to use the version from the core TRAC libs
    mysqlRuntimeOnly (group: 'com.mysql', name: 'mysql-connector-j', version: "$mysql_version") {
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
    }

    mariadbRuntimeOnly group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: "$mariadb_version"
    postgresqlRuntimeOnly group: 'org.postgresql', name: 'postgresql', version: "$postgresql_version"
    sqlserverRuntimeOnly group: 'com.microsoft.sqlserver', name: 'mssql-jdbc', version: "$sqlserver_version"
    oracleRuntimeOnly group: 'com.oracle.database.jdbc', name: 'ojdbc10', version: "$oracle_version"
}


// set up dependencies for SQL driver plugins
// Done here because there isn't an obvious way to reference source sets in plugins.gradle
ext.DEPEND_sql_h2 = sourceSets.h2.runtimeClasspath
ext.DEPEND_sql_mysql = sourceSets.mysql.runtimeClasspath
ext.DEPEND_sql_mariadb = sourceSets.mariadb.runtimeClasspath
ext.DEPEND_sql_postgresql = sourceSets.postgresql.runtimeClasspath
ext.DEPEND_sql_sqlserver = sourceSets.sqlserver.runtimeClasspath
ext.DEPEND_sql_oracle = sourceSets.oracle.runtimeClasspath

// Enable build time dependencies for SQL
// Done here because the DEPEND_ variables aren't visible in the application projects (-svc-meta, deploy-metadb)
ext.enableBuildTimePlugins(["sql"])
