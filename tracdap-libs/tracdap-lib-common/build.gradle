/*
 * Copyright 2020 Accenture Global Solutions Limited
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "java-library"
}

apply from: "${rootProject.projectDir.path}/gradle/base-java.gradle"


description = "TRAC D.A.P. common library, interfaces and utilities used across all TRAC components"

dependencies {

    // Export framework version constraints for 3rd party plugins
    api platform(group: "io.netty", name: "netty-bom", version: "${netty_version}")
    api platform(group: "io.grpc", name: "grpc-bom", version: "${grpc_version}")
    api platform(group: "com.google.protobuf", name: "protobuf-bom", version: "${proto_version}")
    api platform(group: "com.google.guava", name: "guava-bom", version: "${guava_version}")
    api platform(group: "com.fasterxml.jackson", name: "jackson-bom", version: "${jackson_version}")
    api platform(group: "org.slf4j", name: "slf4j-parent", version: "${slf4j_version}")
    api platform(group: "org.apache.logging.log4j", name: "log4j-bom", version: "${log4j_version}")

    api project(':tracdap-api-metadata')
    api project(':tracdap-api-config')
    api project(':tracdap-api-services')

    // Google Guava - common lib includes helpers to translate to/from equivalent JDK types
    api group: 'com.google.guava', name: 'guava', version: "$guava_version"

    // Core protobuf for Java
    api group: 'com.google.protobuf', name: 'protobuf-java', version: "$proto_version"
    api group: 'com.google.protobuf', name: 'protobuf-java-util', version: "$proto_version"

    // gRPC - common lib includes standard wrappers for gRPC stubs and calls
    api group: 'io.grpc', name: 'grpc-api', version: "$grpc_version"
    api group: 'io.grpc', name: 'grpc-stub', version: "$grpc_version"
    api group: 'io.grpc', name: 'grpc-protobuf', version: "$grpc_version"

    // CLI is implemented with Apache Commons CLI
    implementation group: 'commons-cli', name: 'commons-cli', version: "$commons_cli_version"

    // JWT auth
    implementation group: 'com.auth0', name: 'java-jwt', version: "$auth0_jwt_version"

    // Config parser uses Jackson for YAML and JSON
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: "$jackson_version"
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "$jackson_databind_version"
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: "$jackson_version"

    // Logging facade is an API dependency
    api group: 'org.slf4j', name: 'slf4j-api', version: "$slf4j_version"

    // Logging implementation is manipulated in lib-common but not exposed to client code
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: "$log4j_version"
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: "$log4j_version"
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j2-impl', version: "$log4j_version"

    testImplementation project(':tracdap-lib-test')
}


// Compiling dependent targets does not require processResources to be executed by default
// E.g. running tests from the IDE will not trigger processResources - this can cause confusion!
// This dependency ensures resources are always processed, even for partial builds

compileJava.dependsOn(processResources)
