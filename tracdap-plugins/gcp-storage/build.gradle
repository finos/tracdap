/*
 * Copyright 2023 Accenture Global Solutions Limited
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "java-library"
    id 'java-library-distribution'
}

apply from: "${rootProject.projectDir.path}/gradle/base-java.gradle"

description = "GCP storage plugin, store and retrieve data in GCS buckets"

// Put plugins into a separate package group
group = "org.finos.tracdap.plugins"

dependencies {

    api project(':tracdap-lib-common')
    api project(':tracdap-lib-data')

    implementation platform(group: 'com.google.cloud', name: 'libraries-bom', version: "$gcp_bom_version")
    implementation "com.google.cloud:google-cloud-storage"

    testImplementation project(path: ':tracdap-lib-data', configuration: 'testSuite')
}

configurations.configureEach {
    resolutionStrategy.dependencySubstitution {

        // GCP SDK defaults to using grpc-netty-shaded, but TRAC already uses the full Netty stack
        substitute module("io.grpc:grpc-netty-shaded") using module("io.grpc:grpc-netty:${grpc_version}")

        // Do not pull in the JavaX annotations library for licensing reasons
        substitute module("javax.annotation:javax.annotation-api") using project(":tracdap-lib-annotations")
    }
}


// Use a different build dir (don't include plugins in the main release package)
project.buildDir = rootProject.buildDir.path + '/plugins/' + project.name

