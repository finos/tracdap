/*
 * Licensed to the Fintech Open Source Foundation (FINOS) under one or
 * more contributor license agreements. See the NOTICE file distributed
 * with this work for additional information regarding copyright ownership.
 * FINOS licenses this file to you under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with the
 * License. You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "java-library"
}

apply from: "${rootProject.projectDir.path}/gradle/base-java.gradle"

description = "GCP config plugin, load config from GCP cloud storage and secret manager"

// Put plugins into a separate package group
group = "org.finos.tracdap.plugins"

dependencies {

    implementation project(":tracdap-lib-common")

    implementation platform(group: "com.google.cloud", name: "libraries-bom", version: "$gcp_sdk_version")
    implementation "com.google.cloud:google-cloud-storage"

    // Explicit dependency required to configure transport
    implementation "io.grpc:grpc-netty"
}

configurations.configureEach {

    resolutionStrategy.dependencySubstitution {

        // GCP SDK defaults to using grpc-netty-shaded, but TRAC already uses the full Netty stack
        substitute module("io.grpc:grpc-netty-shaded") using module("io.grpc:grpc-netty:${grpc_version}")

        // Do not pull in the JavaX annotations library for licensing reasons
        substitute module("javax.annotation:javax.annotation-api") using project(":tracdap-lib-annotations")
    }
}

// Use a different build dir (don't include plugins in the main release package)
project.buildDir = "${rootProject.buildDir.path}/plugins/${project.name}"
