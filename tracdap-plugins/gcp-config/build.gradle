/*
 * Copyright 2020 Accenture Global Solutions Limited
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "java-library"
}

apply from: "${rootProject.projectDir.path}/gradle/base-java.gradle"


description = "GCP config plugin, load config from GCP cloud storage and secret manager"

// Put plugins into a separate package group
group = "org.finos.tracdap.plugins"

dependencies {

    // Core TRAC library needed for building plugins
    implementation project(':tracdap-lib-common')

    // Version BOM for Google cloud SDK
    implementation platform(group: "com.google.cloud", name: "libraries-bom", version: "$gcp_sdk_version")

    // Pull in the cloud storage library (for config files stored in buckets)
    implementation ("com.google.cloud:google-cloud-storage") {
        // Do not pull in the JavaX annotation JAR, which is licensed under CDDL
        exclude group: 'javax.annotation', module: 'javax.annotation-api'
    }

    // Google cloud libs use the @Generated annotation, which introduces the CDDL license dependency
    // To work around this limitation, TRAC provides a version of just this one annotation class
    implementation project(':tracdap-lib-annotations')

    // Force the version of gRPC for compliance
    implementation platform(group: "io.grpc", name: "grpc-bom", version: "${grpc_version}")

    // Force the version of Jackson (only latest stable will ever pass compliance)
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: "$jackson_version"
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "$jackson_databind_version"
}
